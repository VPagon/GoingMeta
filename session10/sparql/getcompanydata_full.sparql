construct {
    ?company a dbo:Company ;  ?cpred ?cobj ; ?relToPerson ?person ; ?relToPlace ?place .
    ?person a dbo:Person ; rdfs:label ?pobj ; ?relToPersonInv ?company .
    ?place a dbo:Place ;  rdfs:label ?plobj ; ?relToPlaceInv ?place .
}
where {
  # match company by ticker
  ?company a dbo:Company ; dbp:symbol "FB"@en .

  ?company ?cpred ?cobj .
      filter(isLiteral(?cobj) && (lang(?cobj)="en" || lang(?cobj)="") && (strStarts(str(?cpred), "http://dbpedia.org/ontology/") || strStarts(str(?cpred), "http://www.w3.org/2000/01/rdf-schema#label") ))
#related people
optional {
         { ?company ?relToPerson ?person  . filter(strStarts(str(?relToPerson), "http://dbpedia.org/ontology/") && ?relToPerson != dbo:wikiPageWikiLink) }
          union
         { ?person ?relToPersonInv ?company .  filter(strStarts(str(?relToPersonInv), "http://dbpedia.org/ontology/") && ?relToPersonInv != dbo:wikiPageWikiLink) }

         ?person a dbo:Person ;
                       rdfs:label ?pobj .
        filter(lang(?pobj)="en" || lang(?pobj)="")
  }

#related places
optional {
        {  ?company ?relToPlace ?place  . filter(strStarts(str(?relToPlace), "http://dbpedia.org/ontology/") && ?relToPlace != dbo:wikiPageWikiLink) }
         union
        { ?place ?relToPlaceInv ?company . filter(strStarts(str(?relToPlaceInv), "http://dbpedia.org/ontology/") && ?relToPlaceInv != dbo:wikiPageWikiLink) }

         ?place a dbo:Place ;
                       rdfs:label ?plobj .
         filter(lang(?plobj)="en" || lang(?plobj)="")

  }
}
